// Author: cvet, rifleman17
#include "_macros.fos"
#include "_colors.fos"
#include "_msgstr.fos"


#define START_BET             (5)
#define MIN_BET               (1)
#define MAX_BET               (50)
#define STR_ONEHANDED_RING    (2000)

#define MapLimitGet           GetData(60)
#define MapLimitSet #(value)  SetData(60,value)
#define TimeRenewGet          GetData(61)
#define TimeRenewSet #(value) SetData(61,value)
#define RENEW_TIME            (60*60*24*7) // Обновление лимитов карты на автоматы раз в неделю
#define MAX_ONEHANDED_LIMIT   (8000) // Максимальная сумма, которую можно выжать из автоматов(без учета джекпотов)
#define STR_EMPTY             (2001) // Реплика "деньги в автомате закончились"


bool s_Onehanded(Critter& cr, Scenery& onehanded, int skill, Item@ item)
{
	if(skill!=SKILL_PICK_ON_GROUND || (valid(item) && item.GetProtoId()!=PID_BOTTLE_CAPS))
	{
		cr.SayMsg(SAY_NETMSG,TEXTMSG_GAME,STR_USE_NOTHING);
		return true;
	}

	// Сheck map weekly OneHanded limit Expire
	Map@ map=cr.GetMap();
	if (!valid(map)) return true;

	if(map.TimeRenewGet<int(__FullSecond))
	{
		map.MapLimitSet(MAX_ONEHANDED_LIMIT);
		map.TimeRenewSet(__FullSecond+RENEW_TIME);
	}

	int OnehandedMoney=map.MapLimitGet;

	if(OnehandedMoney<=0)
	{
		cr.SayMsg(SAY_NETMSG,TEXTMSG_TEXT,STR_EMPTY);
		return true;
	}

	// Count bet
	uint bet=START_BET;
	if(valid(item))
	{
		// Set all money
		bet=item.GetCount();
	}
	else
	{
		// Check money
		bet=_CritCountItem(cr,PID_BOTTLE_CAPS);
		if(bet<MIN_BET)
		{
			cr.SayMsg(SAY_NETMSG,TEXTMSG_GAME,STR_USE_NOTHING);
			return true;
		}
		if(bet>START_BET) bet=START_BET;
	}

	// Check max bet
	if(bet>MAX_BET) bet=MAX_BET;

	// Set text
	if(valid(map)) map.SetTextMsg(onehanded.HexX,onehanded.HexY,COLOR_GRAY,TEXTMSG_TEXT,STR_ONEHANDED_RING);

	// Calculate
	int gambling=cr.Skill[SK_GAMBLING];
	if(OnehandedMoney<0) gambling+=OnehandedMoney;
	gambling=CLAMP(gambling,6,95);

	// Win
	if(gambling>=Random(1,100))
	{
		// Bonuses
		if(Random(0,99)==77) bet*=10;
		if(Random(0,999)==777) bet*=100;
		if(Random(0,9999)==7777) bet*=1000;

		// Add money
		cr.AddItem(PID_BOTTLE_CAPS,bet);
		if(bet>MAX_BET) cr.Say(SAY_NETMSG,"***JACKPOT***");
		cr.Say(SAY_NETMSG,"+"+bet+"$");
		OnehandedMoney-=bet;
	}
	// Loose
	else
	{
		// Sub money
		cr.DeleteItem(PID_BOTTLE_CAPS,bet);
		cr.Say(SAY_NETMSG,"-"+bet+"$");
		OnehandedMoney+=bet;
	}

	map.MapLimitSet(OnehandedMoney);
	return true;
}


