// FOS Common Sort 100 
// Author: rifleman17

dictionary Defaults;

class ItemDescriptor : Mk2::Descriptor, Mk2::ItemPacket, UpgradeItem::UpgradeDescriptor
#ifdef __SERVER
							, Ai::aiobject
#endif
{
	~ItemDescriptor( )
	{
		//Log( "Destruct item ");
	}
	
	const uint get_MasterId( )
	{
#ifdef __MAPPER
		return RealIt.ParentUID;
#endif
#ifndef __MAPPER
		return ( RealIt.Accessory == ACCESSORY_CRITTER  ? RealIt.CritId : 0 ); 
#endif
	}

	ItemDescriptor( ItemMutual@ item, uint16 pid )
	{
		@RealIt = item;
		Pid = pid;
	}
	
	uint get_Priority( )
	{
		return PriorityDescriptor;
	}
	
	bool IsSet( Mk2::Descriptor@ new )
	{
		return ( new !is null && this.Priority < new.Priority );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item )
	{
		return Item::ItemDescriptor( item, Pid );
	}
	
	string StringId()
	{
		return ( RealIt !is null ?"" + RealIt.Id : "Proto_" + Pid);
	}
	
	string@ get_Name( )
	{
		return "Item_" + StringId();
	}
	
	bool IsUpgrade( ItemMutual& base, ItemMutual& upgrade ) override
	{
		const ProtoItem@  base_proto = GetProtoItem( base.GetProtoId( ) ),
								upgrade_proto = GetProtoItem( upgrade.GetProtoId( ) );
		bool isForTypeUpgr = false;
		switch( upgrade_proto.Upgrade_Type )
		{
			case 1:
				isForTypeUpgr = ( base_proto.Material == upgrade_proto.Material );
				break;
				
			case 2:
				isForTypeUpgr = ( base.GetProtoId() >= uint16( upgrade_proto.Upgrade_TypeValue0 ) && base.GetProtoId() <= uint16( upgrade_proto.Upgrade_TypeValue1 ) );
				break;
				
			default: break;
		}
		return isForTypeUpgr;
	}
	
#ifdef __SERVER
	bool CheckData( UpgradeItem::Data& data, int trigger ) override
	{
		ItemMutual@ upgradeItem = null;
		if( FLAG(RealIt.CraftFlags,CF_UPGRADE_SLOT_2) && FLAG(RealIt.CraftFlags,CF_UPGRADE_SLOT_2_BUSSY) )
		{
			if( RealIt.UpgradeSlot2 != RealIt.Id )
			{
				@upgradeItem = GetItem(RealIt.UpgradeSlot2);
				if(validEngineObject(upgradeItem))
				{
					UpgradeItem::UpgradeDescriptor@ upgrade = cast< UpgradeItem::UpgradeDescriptor@ >( @upgradeItem.ScriptDescriptor );
					if( upgrade !is null && upgrade.GetData( data, trigger ) )
						return true;
				}
			}
		}
		
		if( FLAG(RealIt.CraftFlags,CF_UPGRADE_SLOT_1) && FLAG(RealIt.CraftFlags,CF_UPGRADE_SLOT_1_BUSSY) )
		{
			if( RealIt.UpgradeSlot1 != RealIt.Id )
			{
				@upgradeItem = GetItem(RealIt.UpgradeSlot1);
				if(validEngineObject(upgradeItem))
				{
					UpgradeItem::UpgradeDescriptor@ upgrade = cast< UpgradeItem::UpgradeDescriptor@ >( @upgradeItem.ScriptDescriptor );
					if( upgrade !is null && upgrade.GetData( data, trigger ) )
						return true;
				}
			}
		}
			
		if( FLAG(RealIt.CraftFlags,CF_UPGRADE_SLOT_0) && FLAG(RealIt.CraftFlags,CF_UPGRADE_SLOT_0_BUSSY) )
		{
			if( RealIt.UpgradeSlot0 != RealIt.Id )
			{
				@upgradeItem = GetItem(RealIt.UpgradeSlot0);
				if(validEngineObject(upgradeItem))
				{
					UpgradeItem::UpgradeDescriptor@ upgrade = cast< UpgradeItem::UpgradeDescriptor@ >( @upgradeItem.ScriptDescriptor );
					if( upgrade !is null && upgrade.GetData( data, trigger ) )
						return true;
				}
			}
		}
		return false;
	}
	
	bool UpgradeTrigger_CritterDamage( UpgradeItem::Data& data )
	{
		UpgradeItem::CritterDamageData@ damageData = cast< UpgradeItem::CritterDamageData@ >( @data );
		if( damageData !is null )
		{
			int typeDamage = RealIt.Proto.Upgrade_TriggerValue0,
				damageBorder = RealIt.Proto.Upgrade_TriggerValue1;
			if(damageData.CombatData.Damage[ typeDamage ] > damageBorder )
			{
				int val = damageData.CombatData.ArmorTarget.Deterioration * 100 / MAX_DETERIORATION;
				val = CLAMP( val, 0, 100 );
				
				if(val%101<100)
				{
					damageData.CombatData.Damage[ typeDamage ] = damageBorder;
					Repair::DeteriorateItem(damageData.Critter,damageData.CombatData.ArmorTarget,CLAMP(damageData.TotalDamage,0,damageData.TotalDamage));
					return true;
				}
			}
		}
		else Log( "Error upgrade data" );
		return false;
	}
	
	bool UpgradeTrigger_LineDamage( UpgradeItem::Data& data )
	{
		UpgradeItem::LineDamageData@ lineData = cast< UpgradeItem::LineDamageData@ >( @data );
		if( lineData !is null )
		{
			lineData.Effect = Content::Item::FIRE_LASER_EXPLOSION;
			lineData.IsFire = true;
			lineData.DamageMod *= RealIt.Proto.Upgrade_EffectValue1;
			return true;
		}
		else Log( "Error upgrade data" );
		return false;
	}
	
	bool GetData( UpgradeItem::Data& data, int trigger ) override
	{
		switch( trigger )
		{
			case UpgradeItem::Triggered::CritterDamage:
				if( UpgradeTrigger_CritterDamage( data ) )
					return true;
				else break;
				
			case UpgradeItem::Triggered::LineDamage:
				if( UpgradeTrigger_LineDamage( data ) )
					return true;
				else break;
				
			default: return false;
		}
		return false;
	}
	
	bool CritterUse( Critter& cr, Item& item, Critter@ targetCr, Item@ targetItem, Scenery@ targetScen, uint param )
	{
		int result = 0;
	
		/* // Debug 
		if( cr.IsPlayer() )
		{
			Log( "Critter use item: " + Name );
			
			if( targetCr !is null ) Log( "targetCr " + ( result++ ) );
			if( targetScen !is null ) Log( "targetScen " + ( result++ ) );
			if( targetItem !is null ) Log( "targetItem " + ( result++ ) );
			
		}		
		// */
		
		if( targetCr !is null )
		{
			if( targetCr.Id == cr.Id && CritterUseOnSelf( cr, item, targetCr, targetItem, targetScen, param ) ) return true;
			if( CritterUseOnCritter( cr, item, targetCr, targetItem, targetScen, param ) ) return true;
		}
		if( targetScen !is null )
			if( CritterUseOnScenery( cr, item, targetCr, targetItem, targetScen, param ) ) return true;
		if( targetItem !is null )
			if( CritterUseOnItem( cr, item, targetCr, targetItem, targetScen, param ) ) return true;
		return CritterUseGeneral( cr, item, targetCr, targetItem, targetScen, param );
	}
	
	bool CritterUseOnSelf( Critter& cr, Item& item, Critter@ targetCr, Item@ targetItem, Scenery@ targetScen, uint param )
	{
		return false;
	}
	
	bool CritterUseGeneral( Critter& cr, Item& item, Critter@ targetCr, Item@ targetItem, Scenery@ targetScen, uint param )
	{
		return false;
	}
	
	bool CritterUseOnCritter( Critter& cr, Item& item, Critter@ targetCr, Item@ targetItem, Scenery@ targetScen, uint param )
	{
		return false;
	}
	
	bool CritterUseOnItem( Critter& cr, Item& item, Critter@ targetCr, Item@ targetItem, Scenery@ targetScen, uint param )
	{
		return false;
	}
	
	bool CritterUseOnScenery( Critter& cr, Item& item, Critter@ targetCr, Item@ targetItem, Scenery@ targetScen, uint param )
	{
		return false;
	}
	
#endif
	uint PriorityDescriptor = 0;
	ItemMutual@ RealIt = null;
	uint16 Pid = 0;
}

class ArmorDescriptor : Item::ItemDescriptor, Mk2::CombatArmorPacket
{
	ArmorDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::ArmorDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Armor_" + StringId();
	}
}

class DrugDescriptor : Item::ItemDescriptor
{
	DrugDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::DrugDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Drug_" + StringId();
	}
}

class AmmoDescriptor : Item::ItemDescriptor
{
	AmmoDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::AmmoDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Ammo_" + StringId();
	}
}

class MiscDescriptor : Item::ItemDescriptor
{
	MiscDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::MiscDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Misc_" + StringId();
	}
}

class Misc2Descriptor : Item::ItemDescriptor
{
	Misc2Descriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::Misc2Descriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Misc2_" + StringId();
	}
}

class KeyDescriptor : Item::ItemDescriptor
{
	KeyDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::KeyDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Key_" + StringId();
	}
}

class ContainerDescriptor : Item::ItemDescriptor
{
	ContainerDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::ContainerDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Container_" + StringId();
	}
}

class DoorDescriptor : Item::ItemDescriptor
{
	DoorDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::DoorDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Door_" + StringId();
	}
}

class GridDescriptor : Item::ItemDescriptor
{
	GridDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::GridDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Grid_" + StringId();
	}
}

class GenericDescriptor : Item::ItemDescriptor
{
	GenericDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::GenericDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Generic_" + StringId();
	}
}

class WallDescriptor : Item::ItemDescriptor
{
	WallDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::WallDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Wall_" + StringId();
	}
}

class CarDescriptor : Item::ItemDescriptor
{
	CarDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::CarDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Car_" + StringId();
	}
}

class AnimalDescriptor : Item::ItemDescriptor
{
	AnimalDescriptor( ItemMutual@ item, uint16 pid )
	{
		super( item, pid );
	}
	
	Mk2::Descriptor@ Copy( ItemMutual@ item ) override
	{
		return Item::AnimalDescriptor( item, Pid );
	}

	string@ get_Name( ) override
	{
		return "Animal_" + StringId();
	}
}

void item_init( ItemMutual& item, bool isFirstTime )
{

}

void item_proto_init( ProtoItem& proto )
{
	GetDeafultItemDescriptor( proto );
}

Mk2::Descriptor@ GetDeafultItemDescriptor( ProtoItem@ proto )
{
	return GetDeafultItemDescriptor( null, proto );
}

Mk2::Descriptor@ GetDeafultItemDescriptor( ItemMutual@ item )
{
	return GetDeafultItemDescriptor( item, GetProtoItem( item.GetProtoId() ) );
}

Mk2::Descriptor@ GetDeafultItemDescriptor( ItemMutual@ item, ProtoItem@ proto )
{
#ifdef __MAPPER
	if( item is null || item.MapObjType == 0 )
		return null;
#endif

	Item::ItemDescriptor@ def = null;
	string key = "" + proto.ProtoId;

	if( Defaults.exists( key ) && Defaults.get( key, @def ) ) return @def.Copy( item );
	if( item !is null )
		Log( "Error, item descriptor dont prepare" );
	
	switch( proto.GetType() )
	{
		case ITEM_TYPE_NONE:
			@def = Item::ItemDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_ARMOR:
			@def = Item::ArmorDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_DRUG:
			@def = Item::DrugDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_WEAPON:
			@def = ItemWeapon::WeaponDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_AMMO:
			@def = Item::AmmoDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_MISC:
			@def = Item::MiscDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_MISC2:
			@def = Item::Misc2Descriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_KEY:
			@def = Item::KeyDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_CONTAINER:
			@def = Item::ContainerDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_DOOR:
			@def = Item::DoorDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_GRID:
			@def = Item::GridDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_GENERIC:
			@def = Item::GenericDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_WALL:
			@def = Item::WallDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_CAR:
			@def = Item::CarDescriptor( item, proto.ProtoId );
			break;
		case ITEM_TYPE_ANIMAL:
			@def = Item::AnimalDescriptor( item, proto.ProtoId );
			break;
		default:
		{
			Log( "Warning, uknow protoitem type " + proto.GetType() );
			@def = Item::ItemDescriptor( item, proto.ProtoId );
			break;
		}
	}
	
	Defaults.set( key, @def );
	return def;
}

bool IsUpgrade( ItemMutual& item, ItemMutual& upgrade )
{
	return false;
}

#ifdef __SERVER
// ƒверь, котора€ закроетс€ через 2 игровые минуты илии через некоторое число реальных секунд , указанное в val3
void DoorAutoClose( Item& door, bool firstTime)
{
	door.SetEvent(ITEM_EVENT_SKILL,"Item::e_UseAutoCloseDoor");
}

bool e_UseAutoCloseDoor( Item& door, Critter& cr, int skill)
{
	CreateTimeEvent( __FullSecond + REAL_MINUTE(door.Val3==0? 2 : door.Val3), "Item::e_AutoCloseDoor", door.Id, true);
	return false;
}

uint e_AutoCloseDoor( uint[]@ val)
{
	Item@ door = GetItem(val[0]);
	if( door is null ) return 0;

	if (!FLAG(door.LockerCondition,LOCKER_ISOPEN)) return 0;
	uint16 x=0, y = 0;
	
	Map@ map = door.GetMapPosition(x, y);
	if( map is null ) return 0;

	Critter@ cr = map.GetCritter(x,y);
	if( cr !is null )
	{
		if(cr.IsLife())
			return REAL_MINUTE(3);
		else // ѕопытка сдвинуть труп. ≈сли не получитс€, дверь поверх трупа закроетс€		
			cr.TransitToMap(map.Id, x-1,y,cr.Dir);
	}

	if(door.LockerClose()) return 0;

	return REAL_MINUTE(3);
}

// ƒверь, котора€ не открываетс€ стандартными способами
void ClosedDoor( Item& door, bool firstTime)
{
	door.SetEvent(ITEM_EVENT_SKILL,"Item::_UseDoor");
}

bool _UseDoor( Item& door, Critter& cr, int skill)
{
	return true;
}

// ƒверь, при использовании которой открываетс€ диалог
void DialogDoor( Item& door, bool firstTime)
{
	door.SetEvent(ITEM_EVENT_SKILL,"Item::_UseDialogDoor");
}

bool _UseDialogDoor( Item& door, Critter& cr, int skill)
{
	uint16 x = 0, y = 0;
	Map@ map = door.GetMapPosition( x, y );
	Mk2RunDialog(cr, door.Val3, map, x, y, false);
	return true;
}

// √олодиск с номером, присвоенном в Val0
void Holo( Item & item, bool firstTime)
{
	if(item.GetProtoId()!=Content::Item::HOLODISK) return;
	item.HolodiskNumber=item.Val0;
	item.SetScript("");
	item.Update();

}

// Ќастройка автоматического удалени€ предмета через икс часов
void SetDeleteItem( Item& item, uint delHours)
{
	if(! IsAnyData("DeleteItem_"+item.Id))
	{
		uint[] data = {item.Id};
		if( !SetAnyData("DeleteItem_"+item.Id,data))
			Log("AnyData not created");
		else
			CreateTimeEvent( __FullSecond + delHours*60*60, "Item::e_DeleteItem", data, true);
	}
	item.SetScript("Item::AutoDelete");
}

uint e_DeleteItem( uint[]@ val)
{
	if( val !is null )
	{
		Item@ item = GetItem(val[0]);
		if(validEngineObject(item))
		{
			if(item.GetCount()>1) item.SetCount(item.GetCount()-1);
			else DeleteItem(item);
		}
		if( IsAnyData("DeleteItem_"+val[0]))
			EraseAnyData( "DeleteItem_"+val[0] );
	}
	return 0;
}

void AutoDelete( Item& item, bool firstTime)
{
	if(!firstTime) 
	{
		DeleteItem(item);
		if( IsAnyData("DeleteItem_"+item.Id))
			EraseAnyData( "DeleteItem_"+item.Id );
	}
}

// ќхран€ема€ дверь или другой предмет
// в Val0 - пишетс€ врем€ автоматического закрыти€ двери, в реальных минутах, если больше 0, дверь будет автоматически закрыватьс€
// обратите внимание, игрок может застр€ть за дверью, если пройдет внутрь, а она захлопнетс€
// в Val1 прописываетс€ номер роли Ќѕ÷ которые будут говорить, должно быть отличным от нол€
// если скрипт только на одно использование - в Val2 - нужно указать любое значение, это может использоватьс€ дл€ охран€емых предметов, например оружи€
#define STR_DONTOUCH     (540)


void GuardedItem( Item& item, bool firstTime)
{
	item.SetEvent(ITEM_EVENT_SKILL,"Item::_GuardedItemSkill");
}

bool _GuardedItemSkill( Item& item, Critter& cr, int skill)
{
	Map@ map=cr.GetMap();
	if ( map is null ) return false;
	
	int  role=item.Val1;
	bool reset=(item.Val2 > 0 ? true : false);
	if(cr.IsPlayer())
	{
		uint guards=map.GetNpcCount(role, FIND_LIFE|FIND_ONLY_NPC);
		if(guards==0)
		{	
			if(reset)
			{
				item.SetEvent(ITEM_EVENT_SKILL, "");
				item.SetScript(null);
			}
			if(item.GetType()==ITEM_TYPE_DOOR || item.GetType()==ITEM_TYPE_CONTAINER)
			{
				if(item.Val0 > 0)
					CreateTimeEvent( __FullSecond + REAL_MINUTE(item.Val0==0? 2 : item.Val0), "Item::e_AutoCloseDoor", item.Id, true);
			}
			return false;
		}
		Critter@ guard=map.GetNpc(role, FIND_LIFE|FIND_ONLY_NPC, Random(0, guards-1));
		if( guard !is null )
		{
			GameVar@ counter = GetUnicumVar(UVAR_guarded_item_skill, guard.Id, cr.Id);
			if( counter !is null )
			{
				if(counter<2)
				{
					NpcPlane@[] planes = {};
					if(guard.GetPlanes(AI_PLANE_ATTACK, planes) > 0)
					{
						for(uint i=0, iMax=planes.length(); i<iMax; i++)
						{
							if(planes[i].Attack_TargId==cr.Id)
							{
								if(reset)
								{
									item.SetEvent(ITEM_EVENT_SKILL, "");
									item.SetScript(null);
								}
								return false;
							}
						}
					}
					counter=counter.GetValue() + 1;
					guard.SayMsg(SAY_SHOUT, TEXTMSG_TEXT, STR_DONTOUCH, "$pname" + GetPlayerName(cr.Id));
				}
				else
				{
					counter=0;
					NpcPlanes::AddAttackPlane(guard, 0, cr);
				}
			}
		}		
		return true;
	}
	else
		// Ќпц такие двери открыть тоже не смогут
		return true;
}

void Activation( Item& item, bool firstTime)
{
	//item.SetEvent(ITEM_EVENT_USE, "Item::_useActivativeItem");
}

bool ChangeProto( Item& item, uint pid )
{
	if( GetProtoItem( pid ) is null )
	{
		Log( "Ќевалиден прототип предмета под пидом: " + pid );
		return false;
	}
	
	return item.ChangeProto( pid );
}

bool _useActivativeItem( Item& item, Critter& crit, Critter@ onCritter, Item@ onItem, Scenery@ onScenery) //Ќе переносить, не переименовывать, не смотреть. Ѕиндитс€ из натива.
{
	if( onCritter !is null || onItem !is null || onScenery !is null ||item.Deterioration>=MAX_DETERIORATION) return false;
	ChangeProto( item, item.GetProtoId()+1 );
	//item.SetEvent(ITEM_EVENT_USE, "_useDeactivativeItem");
	if(item.IsDeteriorable()) item.Val1=__FullSecond;
	item.Update();
	return true;
}

void Deactivation( Item& item, bool firstTime)
{
	item.SetEvent(ITEM_EVENT_USE, "Item::_useDeactivativeItem");
}

bool _useDeactivativeItem( Item& item, Critter& crit, Critter@ onCritter, Item@ onItem, Scenery@ onScenery)
{
	if( onCritter !is null || onItem !is null || onScenery !is null ) return false;
	ChangeProto( item, item.GetProtoId()-1 );
	//item.SetEvent(ITEM_EVENT_USE, "_useActivativeItem");
	item.Update();
	return true;
}

void ChargedItem( Item& item, bool firstTime)
{
	if(firstTime) 
		item.Charge=100;
}

void GetParams( Critter& cr, Item& item )
{
	int[] data = { 
		item.Deterioration, 
		item.BrokenCount, 
		item.BrokenFlags, 
		item.RadioChannel, 
		item.LockerId, 
		item.Info, 
		item.AmmoCount, 
		item.HolodiskNumber		
		};
		
	cr.RunClientScript( "Mk2@Item::SetParams", item.Id, 0, 0, item.GetLexems(), data );
}

void ModuleInit( )
{
	
}

#endif

#ifdef __CLIENT

Sprite::Sprite@ CombinationTemplate;

namespace Option
{
	namespace GUI
	{
		int Width = 0;
		
		uint ColorFocus = COLOR_LEMON;
		uint ColorDown = COLOR_WHITE;
		
		string CombinationTemplate = "ItemCombinationTempl.png";
		
		void ModuleInit( )
		{
			@ThisModule::CombinationTemplate = Sprite::Sprite( CombinationTemplate, PT_ART_INTRFACE );
		}
	}
}

Interface@ lmenu_show_item = null;

bool LMenuShow( uint& loop )
{
	LMenu::Instance@ lmenu = LMenu::Prepare( LMenuResult );
	ItemCl@ item = lmenu_show_item.Item;
		
	if( lmenu_show_item.Type == ITEM_TYPE_WEAPON )
	{
		if( item !is null && item.AmmoCount > 0 )
			lmenu.ShowNode( "Unload" );
	}
			
	if( item !is null && ( FLAG( item.Flags, ITEM_CAN_USE ) || FLAG( item.Flags, ITEM_CAN_USE_ON_SMTH ) ) )
		lmenu.ShowNode( "Use" );
			
	lmenu.ShowNode( "Skill" )
		//.ShowNode( "SortUp" )
		//.ShowNode( "SortDown" )
		.ShowNode( "Drop" )
		.ShowNode( "Cancel" )
		.Show( lmenu_show_item );
	loop = 0;
	return true;
}

class Interface : IGUIListingElement, IGUIDragAndDropElement, Splitter::SplitResult, IGUIDescriptor
{
	void Show( ) override
	{
		
	}
	
	void Hide( ) override
	{
		
	}
	
	bool Refresh( int lookType, IGUIDescriptor@ new ) override
	{
		return true;
	}
	
	bool Drag( ) override
	{
		return true;
	}
	
	void Drop( ) override
	{
	
	}
	
	void Split( const uint& count ) override
	{
		@Transfer = ThisModule::TransferData();
		Transfer.IsSplit = true;
		Here.Transfer( this, count, null );
		@Transfer = null;
	}
	
	uint get_SplitMaxCount( ) override
	{
		return Count;
	}
	
	Interface( ItemCl& item, Item::ItemLocation@ here )
	{
		CritSlot = item.CritSlot;
		Id = item.Id;
		CritId = item.CritId;
		Proto = item.GetProtoId();
		Count = item.GetCount();
		DrawSlotName = true;
		LookType = ITEM_LOOK_BARTER; //ITEM_LOOK_ONLY_NAME;
		Type = item.GetType();

		@Image = Sprite::Sprite();
		Image.LoadItem( Proto );
		
		@Here = here;
	}
	
	Interface( ThisModule::Interface& item, uint count ) override
	{
		CritSlot = item.CritSlot;
		Id = item.Id;
		CritId = item.CritId;
		Proto = item.Proto;
		Count = count;
		DrawSlotName = item.DrawSlotName;
		item.Count -= count;
		LookType = ITEM_LOOK_BARTER; //ITEM_LOOK_ONLY_NAME;
		
		@Image = Sprite::Sprite();
		Image.LoadItem( Proto );
		
		@Here = item.Here;
		Type = Type;
	}
	
	ProtoItem@ get_ProtoItem( )
	{
		return GetProtoItem( Proto );
	}
	
	void after_Draw( Item::Interface@ swapItem, int x, int y )
	{
	
	}
	
	void Draw( int x, int y ) override
	{
		Image.Draw( x - int( Image.Width * 0.5 ) , y - int( Image.Height * 0.5 ) );
		ThisModule::Interface@ swapItem = cast<ThisModule::Interface@>(@GUI_GetFocusListingElement( ));
		if( this is swapItem )
			@swapItem = null;
		
		after_Draw( swapItem, x, y );
	}
	
	void Draw( int x, int y, int w, int h ) override
	{
		Image.Draw( x, y, w, h );
	}
	
	void Draw( int x, int y, int w, int h, uint c ) override
	{	
		ItemCl@ this_item = GetItem( Id );
		if( this_item !is null && Count == 0 )
			Count = this_item.GetCount();
		if( Count == 0 )
			return;
			
		if( GUI_GetDragAndDrop( ) is this )
			c = ThisModule::Option::GUI::ColorFocus;
		
		Image.Draw( x, y, w, h, VISUAL_FLAG_ZOOM, c );
		
		if( Count > 1 )
			DrawText( "x" + Count, x, y, w, h, COLOR_WHITE, FONT_FALLOUT, FT_ALIGN | FT_BORDERED );
			
		string slotText = "";
		if( CritSlot != SLOT_INV )
		{
			if( DrawSlotName ) 
				switch( CritSlot )
				{
					case SLOT_HAND1:
						slotText = "hand";
						break;
						
					case SLOT_HAND2:
						slotText = "hand2";
						break;
						
					case SLOT_ARMOR:
						slotText = "armor";
						break;
						
					case SLOT_BONUS:
						slotText = "bonus";
						break;
						
					default: break;
				}
		}
		CritterCl@ chosen = GetChosen( );
		if( chosen !is null && chosen.Param[ ST_ADMINGUI ] == 1 )
			slotText += " " + Id;
			
		if( slotText != "" )
			DrawText( slotText, x, y + ( h - 10 ), w, h, COLOR_WHITE, FONT_FALLOUT, FT_ALIGN | FT_BORDERED );
	}
	
	void Focus( ) override
	{
		if( InformerDescriptor !is this && Item !is null )
			SetItemInformer( LookType, GetDescription( Count, LookType ), Item, Item.Lexems, Item.Info, Proto, this );
	}
	
	string@ GetDescription( uint count, int lookType )
	{	
		ProtoItem@ proto = this.ProtoItem;

		ItemCl@ item = Item;
		if( item is null ) return "";
		// Default error text
		if( !IsMsgStr( TEXTMSG_ITEM, Proto * 100 + item.Info + 1 ) )
			return GetMsgStr( TEXTMSG_GAME, STR_ITEM_LOOK_NOTHING );
		
		string result = "";

		if( lookType == ITEM_LOOK_ONLY_NAME )
			result += ReplaceText( GetMsgStr( TEXTMSG_GAME, STR_ITEM_LOOK ), "NAME", GetMsgStr( TEXTMSG_ITEM, Proto * 100 + item.Info ) );
		else
		{
		   result += GetMsgStr( TEXTMSG_ITEM, Proto * 100 + item.Info ) + "\n";

			if( Proto == uint( Content::Item::HOLODISK ) && item.HolodiskNumber != 0 )
				result += GetMsgStr( TEXTMSG_HOLO, STR_HOLO_INFO_NAME( item.HolodiskNumber ) );
			else
				result += GetMsgStr( TEXTMSG_ITEM, Proto * 100 + item.Info + 1 );

			// Weight
			result += "\n" + ReplaceText( GetMsgStr( TEXTMSG_GAME, STR_ITEM_WEIGHT_GRAMM ), "VALUE", proto.Weight * count );

			// Ammo load
			if( proto.Type == ITEM_TYPE_WEAPON && proto.Weapon_MaxAmmoCount > 0 )
			{
				result += "\n";
				string str = GetMsgStr( TEXTMSG_GAME, STR_INV_HAS_SHOTS );
				str = ReplaceText( str, "VALUE",     item.AmmoCount );
				str = ReplaceText( str, "MAX_VALUE", proto.Weapon_MaxAmmoCount );
				str = ReplaceText( str, "AMMO",      GetMsgStr( TEXTMSG_GAME, STR_CALIBER( proto.Weapon_Caliber ) ) );
				result += str;
			}

			if( proto.Deteriorable )
			{
				// Broken level, procent
				uint8 brokenFlags = item.BrokenFlags;
				result += "\n";
				if( FLAG( brokenFlags, BI_NOTRESC ) )
					result += GetMsgStr( TEXTMSG_GAME, STR_DETERIORATION_NO_RESC );
				else if( FLAG( brokenFlags, BI_LOWBROKEN ) )
					result += GetMsgStr( TEXTMSG_GAME, STR_DETERIORATION_BROKEN_LOW );
				else if( FLAG( brokenFlags, BI_NORMBROKEN ) )
					result += GetMsgStr( TEXTMSG_GAME, STR_DETERIORATION_BROKEN_NORM );
				else if( FLAG( brokenFlags, BI_HIGHBROKEN ) )
					result += GetMsgStr( TEXTMSG_GAME, STR_DETERIORATION_BROKEN_HIGH );
				else
					result += ReplaceText( GetMsgStr( TEXTMSG_GAME, STR_DETERIORATION_PROCENT ), "VALUE", item.Deterioration * 100 / MAX_DETERIORATION );
				result += ( " / " + ( ( ( ( proto.MaxRepairCount > 0 ) ? ( proto.MaxRepairCount ) : 30 ) + 10 ) * 10 - item.RadioChannel ) + "%" );
				
				// Service
				if( FLAG( brokenFlags, BI_SERVICE ) )
					result += "\n" + GetMsgStr( TEXTMSG_GAME, STR_DETERIORATION_SERVICE );

				// Service ext
				if( FLAG( brokenFlags, BI_SERVICE_EXT ) )
					result += "\n" + GetMsgStr( TEXTMSG_GAME, STR_DETERIORATION_SERVICE_EXT );

				// Broken count
				CritterCl@ chosen = GetChosen();
				if( chosen !is null && chosen.Perk[ PE_MR_FIXIT ] != 0 )
					result += "\n" + ReplaceText( GetMsgStr( TEXTMSG_GAME, STR_DETERIORATION_BROKEN_COUNT ), "VALUE", item.BrokenCount );
			}

			// Key id
			if( proto.Type == ITEM_TYPE_KEY )
				result += "\n" + ReplaceText( GetMsgStr( TEXTMSG_GAME, STR_INV_KEY_NUMBER ), "KEY_ID", item.LockerId );
			result += "\n          ";
		}

		// Format tags
		if( item.Lexems != "" )
			result = FormatTags( result, item.Lexems );

		while( result.length() != 0 && ( substring( result, result.length() - 1, 1 ) == "\n" || substring( result, result.length() - 1, 1 ) == " " ) )
			result = substring( result, 0, result.length() - 1 );

		return result;
	}
	
	void LoseFocus( ) override
	{
		//LookType = ITEM_LOOK_ONLY_NAME;
		if( InformerDescriptor is this )
			SetItemInformer( -1, "", null, "", 0, 0, null );
		if( lmenu_show_item is this )
		{
			@lmenu_show_item = null;
			__EventLoop.Unsubscribe( @LMenuShow );
		}
	}
	
	void ClickUp( int click ) override
	{
		if( lmenu_show_item is this )
		{
			@lmenu_show_item = null;
			__EventLoop.Unsubscribe( @LMenuShow );
		}
	}
	 
	void ClickDown( int click ) override
	{
		if( MOUSE_CLICK_LEFT == click )
		{
			SetItemInformer( -1, "", null, "", 0, 0, null );
			if( !CtrlPressed )
				GUI_SetDragAndDrop( cast<IGUIDragAndDropElement@>(@this) );
			else Here.Transfer( this, Count, null );
		}
		else if( MOUSE_CLICK_RIGHT == click )
		{
			__EventLoop.Subscribe( @LMenuShow, 400 );
			@lmenu_show_item = this;
		}
	}
	
	void MouseMove( int x, int y ) override
	{
		if( Item !is null )
			SetItemInformer( LookType, GetDescription( Count, LookType ), Item, Item.Lexems, Item.Info, Proto, this );
	}
	
	Sprite::Sprite@ Image;
	
	ItemCl@ get_Item ()
	{
		if( CritId != 0 )
		{
			CritterCl@ critter = GetCritter( CritId );
			if( critter !is null )
			{
				ItemCl@ item = critter.GetItemById( Id );
				if( item !is null )
					return item;
			}
		}
		return GetItem( Id );
	}
	
	uint Proto;
	uint Count;
	uint Id;
	int Type;
	
	uint CritId;
	int LookType;
	int CritSlot;
	
	bool DrawSlotName = true;
	
	ItemLocation@ Here;
	TransferData@ Transfer = null;
	
}

class TransferData
{
	TransferData(){
		IsSplit = false;
	}

	
	bool IsSplit;
}

void LMenuResult( LMenu::Instance@ lmenu, string& result )
{
	Item::Interface@ item = cast<Item::Interface@>(@lmenu.Owner);
	if( item is null )
		return;
		
	if( result == "Unload" )
	{
		uint[] actions = { CHOSEN_USE_ITEM, item.Id, 0, TARGET_SELF_ITEM, uint( -1 ), USE_RELOAD, 0 };
        SetChosenActions( actions );
	}
	else if( result == "Drop" )
	{
		if( !GetChosen().IsLife() || !GetChosen().IsFree() )
			return;
		/*if( item.GetCount() > 1 )
			SplitStart( cont_item, SLOT_GROUND | ( ( TargetSmth.GetParam() ? 1 : 0 ) << 16 ) );
		else*/
		uint[] actions = { CHOSEN_MOVE_ITEM, item.Id, item.Count, SLOT_GROUND, 0, 0, 0 };
        SetChosenActions( actions );
		
			/*			
    Master.ShowNode( "Use" );
    Master.ShowNode( "Skill" );
    Master.ShowNode( "SortUp" );
    Master.ShowNode( "SortDown" );
			*/
	}
	else if( result == "Use" )
	{
		// #define CHOSEN_USE_ITEM           (6)  // Item id, Item pid, Target type (see below), Target id, Item mode (see below), Some param (timer)
		uint[] actions = { CHOSEN_USE_ITEM, item.Id, 0, TARGET_SELF, GetChosen().Id, USE_USE, 0 };
        SetChosenActions( actions );
		
	}
	else if( result == "Skill" )
	{
		SkillBox::Show( item.Id );
	}
	else if( result == "SortUp" ){}
	else if( result == "SortDown" ){}
}

interface ItemLocation
{
	void Transfer( IGUIListingElement@ item, uint count, Item::ItemLocation@ exchange );
	Item::ItemLocation@ opAddAssign( Item::Interface& item );
}

#endif 
