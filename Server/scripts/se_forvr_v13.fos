// Autor: Cracker
// Guardian of Forever special encounter scripts

#include "_macros.fos"
#include "_colors.fos"

#define STR_COMPUTER_MESSAGE (70000)
#define STR_COMPUTER_BEEP    (70001)
#define MESSAGE_HEX_X        (74)
#define MESSAGE_HEX_Y        (127)
#define MESSAGE_COLOR        (COLOR_RGB(0xFF, 0, 0xFF))

void _TranInit(Item& item, bool firstTime)
{
	item.SetEvent(ITEM_EVENT_WALK, "_Transit");
}

void t_Zizz(Critter& cr, Scenery& trigger, bool entered, uint8 dir)
{
	CreateTimeEvent(__FullSecond, "e_Zizz", cr.GetMapId(), false);
}

uint e_Zizz(uint[]@ values)
{
	Map@ map=GetMap(values[0]);
	if(not valid(map)) return 0;
	map.SetTextMsg(MESSAGE_HEX_X, MESSAGE_HEX_Y, MESSAGE_COLOR, TEXTMSG_TEXT, STR_COMPUTER_BEEP);
	return REAL_MINUTE(1);
}

void _Transit(Item& item, Critter& cr, bool entered, uint8 dir)
{
	if(not entered || not cr.IsPlayer()) return;
	Map@ map=cr.GetMap().GetLocation().GetMapByIndex(1);
	cr.TransitToMap(map.Id, 0);
	DeleteItem(item);
}

bool s_Terminal(Critter& cr, Scenery& terminal, int skill, Item@ item)
{
	if(skill==SKILL_PICK_ON_GROUND && not valid(item))
	{
		Map@ map=cr.GetMap().GetLocation().GetMapByIndex(0);
		cr.TransitToMap(map.Id, 0);
		cr.SayMsg(SAY_NETMSG, TEXTMSG_TEXT, STR_COMPUTER_MESSAGE);
		cr.StatBase[ST_EXPERIENCE] +=1000;
		return true;
	}
	return false;
}

void Test(Critter& cr, int mapIndex, int, int)
{
	uint locId=CreateLocation(LOCATION_SpecialGuardianEncounter, cr.WorldX, cr.WorldY, null);
	Location@ loc=GetLocation(locId);
	Map@ map=loc.GetMapByIndex(mapIndex);
	if(valid(map)) cr.TransitToMap(map.Id, 0);
}
