#include "_macros.fos"
#include "_msgstr.fos"
#include "utils_for_array.fos"

// Author: rifleman17
// NoPvp режим для некоторых карт на сервере
// Если включен при первом запуске, нежелательно отключать без вайпа, иначе смешаются пвп и нопвп персонажи

uint[] noPvpMaps = { MAP_replication1, MAP_replication2, MAP_replication3, MAP_replication4, MAP_replication_hell };

const int[] excludeDialogs =
{
    DIALOG_mod_aureliano, DIALOG_arroyo_gayzum, DIALOG_arroyo_laumer, DIALOG_arroyo_selma, DIALOG_vc_mutant_charley, DIALOG_ncr_westin, DIALOG_ncr_smit, DIALOG_sf_citizen_shue, DIALOG_vc_shrim, DIALOG_vc_kohl, DIALOG_vc_randal, DIALOG_vc_ed, DIALOG_sf_kuraru, DIALOG_sf_bruce, DIALOG_sf_khung, DIALOG_sf_wei, DIALOG_sf_imbot, DIALOG_sf_master_khung, DIALOG_sf_kuraru, DIALOG_redd_wade, DIALOG_redd_savinel, DIALOG_redd_stanwell, DIALOG_ncr_hal,
    DIALOG_bh_kit, DIALOG_ncr_hate, DIALOG_vc_shrim, DIALOG_vc_guard_fighter
};
const int[] btInvul = { BT_MEN, BT_WOMEN, BT_CHILDREN, BT_SUPER_MUTANT, BT_GHOUL };

#define NOPVP_VAR        ( LMVAR_is_nopvp_map ) // Номер переменной, в которой
#define CTE_NOPVP_OFF    ( 42 )                 // Идентификатор события "очистить нопвп режим"
#define TIME_CHECK       ( REAL_MINUTE( 1 ) )   // Время проверки, не вышел ли игрок с нопвп зоны


void InitNoPvpMaps()
{
	if( !__NewbieLocationIsPvp )
	{
		noPvpMaps.insertLast( MAP_newbie_replication );
		noPvpMaps.insertLast( MAP_newbie_workzone );
		noPvpMaps.insertLast( MAP_newbie_entrance );
		noPvpMaps.insertLast( MAP_newbie_farm );
	}
}

void OnCitterMapIn( Critter& cr, Map& map )
{
    cr.ModeBase[ MODE_NO_PVP ] = 0;   // перестраховка, по идее это присваивание не нужно, но на всякий случай...
    if( cr.IsPlayer() && IsNoPvpMap( map ) )
    {
        cr.ModeBase[ MODE_NO_PVP ] = 1;
        cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, 1 );
        if( cr.GetTimeEvents( CTE_NOPVP_OFF, null, null, null ) == 0 )
        {
            cr.AddTimeEvent( "cte_NoPvpOff", TIME_CHECK, CTE_NOPVP_OFF );
        }
    }
}

uint cte_NoPvpOff( Critter& cr, int identifier, int& rate, int& )
{
    if( cr.Mode[ MODE_NO_PVP ] == 0 )
        return 0;
    Map@map = cr.GetMap();
    if( valid( map ) && IsNoPvpMap( map ) ) // если карта с включенным НоПвп, откладываем снятие режима
        return TIME_CHECK;
    cr.ModeBase[ MODE_NO_PVP ] = 0;
    return 0;
}

void OnCritterMapOut( Critter& cr, Map& map )
{
    Map@ mapCr = cr.GetMap();
    if( valid( mapCr ) && IsNoPvpMap( mapCr ) )
        return;
    if( cr.IsPlayer() && cr.Mode[ MODE_NO_PVP ] > 0 ) // если но пвп карты не включены, персонаж получил нопвп режим другим способом, не отменяем
    {
        cr.ModeBase[ MODE_NO_PVP ] = 0;
        cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, 2 );
    }
}

bool IsNoPvpMap( Map& map )
{
    return noPvpMaps.find( map.GetProtoId() ) != -1;
}

void SetNoPvp()
{
    Map@ map = null;
    for( uint i = 0, l = noPvpMaps.length(); i < l; i++ )
    {
        @map = GetMapByPid( noPvpMaps[ i ], 0 );
        if( valid( map ) )
            SetNoPvp( map, true );
    }
}

// Установка NoPvp режима на карте, действует только при первой загрузке карты
// Присваивается переменная, а также все НПЦ на карте получают режим MODE_INVULNERABLE
void SetNoPvp( Map& map, bool firstTime )
{
    if( firstTime )
    {
        if( -1 != noPvpMaps.find( map.GetProtoId() ) )
        {
            Critter@[] critters;
            Critter@ cr;
			
            if( map.GetCritters( 0, FIND_ALL | FIND_ONLY_NPC, critters ) > 0 )
            {
                for( uint i = 0, l = critters.length(); i < l; i++ )
                {
                    @cr = critters[ i ];
                    if( !valid( cr ) || cr.IsPlayer() )
                        continue;
						
                    if( -1 != excludeDialogs.find( cr.Stat[ ST_DIALOG_ID ] ) )
                        continue;
						
                    if( -1 != btInvul.find( cr.Stat[ ST_BODY_TYPE ] ) )
                        cr.ModeBase[ MODE_INVULNERABLE ] = 1;                     // God Mode On.
                }
            }
        }
    }
}
